import com.android.build.gradle.tasks.MergeSourceSetFolders
import com.nishtahir.CargoBuildTask

plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.mozilla.rust-android-gradle.rust-android"
}

android {
    namespace "rs.tts"
    compileSdkVersion 33
    ndkVersion "26.1.10909125"
    defaultConfig {
        applicationId "rs.tts"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.2.0"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "com.google.android.material:material:1.1.0"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
}

cargo {
    module = "."
    libname = "hello_world"
    targets = ["arm64"]
//    targets = ["arm", "x86"]
}

project.afterEvaluate {
    tasks.withType(CargoBuildTask).forEach { buildTask ->
        tasks.withType(MergeSourceSetFolders).configureEach {
            it.inputs.dir(layout.buildDirectory.dir("rustJniLibs" + File.separatorChar + buildTask.toolchain.folder))
            it.dependsOn(buildTask)
        }
    }
}
